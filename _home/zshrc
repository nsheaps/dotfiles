#!/bin/zsh
# sourced when an interactive shell is spawned. Use to customize the feel of
# your terminal, but scripts run outside the terminal may not use this setup

# Make sure dotfiles dir is set, or exit with error
if [[ -z "${DOTFILES_DIR:-}" ]]; then
  echo "Error: DOTFILES_DIR is not set. Cannot source zshrc." >&2
  return 1
fi

# Enable comments in interactive shells (allows copy-pasting commands with comments)
setopt interactivecomments

# .zshenv is loaded before anything else, including macos path setup using /etc/paths (which then trump stuff in zshenv)
# .zprofile is loaded at login shells (when macos boots)
# .zshrc is loaded at non-login interactive shells (when you open a terminal)
#   in vscode, zshrc may be loaded again

# User-customizable section
# Add your personal customizations here


# Lazy-load (autoload) Zsh function files from a directory.
ZFUNCDIR=${ZDOTDIR:-$HOME}/.zfunctions
if [[ -d "$ZFUNCDIR" ]]; then
  fpath=($ZFUNCDIR $fpath)
  autoload -Uz $ZFUNCDIR/*(.:t) 2>/dev/null
fi

# Antidote plugin management
# Plugins are defined in _home/zsh_plugins.txt, compiled to ~/.zsh_plugins.zsh
zsh_plugins_src="${DOTFILES_DIR}/_home/zsh_plugins.txt"
zsh_plugins_compiled="${HOME}/.zsh_plugins.zsh"

# Lazy-load antidote from its functions directory (supports Apple Silicon and Intel Macs)
if (( $+commands[brew] )); then
  _antidote_dir="$(brew --prefix antidote 2>/dev/null)/share/antidote"
  if [[ -d "$_antidote_dir/functions" ]]; then
    fpath=("$_antidote_dir/functions" $fpath)
    autoload -Uz antidote
  fi
  unset _antidote_dir
fi

# Generate compiled plugins file when source changes
if [[ ! -f "$zsh_plugins_compiled" ]] || [[ "$zsh_plugins_src" -nt "$zsh_plugins_compiled" ]]; then
  antidote bundle <"$zsh_plugins_src" >"$zsh_plugins_compiled"
fi

# Source the compiled plugins
source "$zsh_plugins_compiled"


# Source all profile.d scripts (environment setup)
for script in "${DOTFILES_DIR}"/_home/profile.d/*.sh(N); do
  source "$script"
done

# Source all interactive.d scripts (functions, aliases) - only for TTY
if [[ -t 1 ]]; then
  for script in "${DOTFILES_DIR}"/_home/interactive.d/*.sh(N); do
    source "$script"
  done
fi

